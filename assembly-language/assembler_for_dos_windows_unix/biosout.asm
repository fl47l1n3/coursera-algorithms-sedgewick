; Вывод всех без исключения символов, включая символы перевода строки.
; Средствами BIOS

        .model  tiny
        .code
        org     100h
start:  
        ; Вызываем функцию BIOS - установить видеорежим
        ; AX=0003
        ; AH=00 - установить видеорежим
        ; AL=03 - очистка экрана и установка курсора в 0,0
        mov     ax,0003h
        int     10h             ; Прерывание BIOS
        
        mov     dx,0            ; DH,DL будут использоваться для установки положения курсора. Очищаем в 0,0
        mov     si,256          ; Счетчик цикла
        
        ; Вызываем функцию BIOS - вывести символ с аттрибутом на экран
        ; AL - ASCII код символа
        ; AH - 09 код функции
        ; CX - число повторений, 1 раз
        ; BL - аттрибут 
        mov     al,0
        mov     ah,9
        mov     cx,1
        mov     bl,00011111b
cloop:
        int     10h         ; Выводим
        push    ax          ; Сохраняем текущий символ и номер функции (пока вызываем другую функцию)
                            
        ; ================= Делаем =>SPACE=> ===============
        ; Переместить курсор
        ; AH - код 2
        ; DL - столбец++
        mov     ah,2
        inc     dl
        int     10h
        
        ; Вывести пробел
        ; AH - 09 код AL = 20h пробел
        mov     ax,0920h
        int     10h                  
        
        ; Переместить курсор
        mov     ah,2
        inc     dl
        int     10h                                         
        ; ===================================================
        
        pop     ax              ; Можем продолжить печатать наши символы
        
        ; Следующий символ
        inc     al
        test    al,0Fh
        jnz     continue_loop   
        
        push    ax
        mov     ah,2
        inc     dh
        mov     dl,0
        int     10h
        pop     ax
        
continue_loop:
        dec     si
        jnz     cloop   ; CX используется внутри цикла, так то нельзя использовать команду loop

        ret
        end     start